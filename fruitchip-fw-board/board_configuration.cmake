set(BOARD_CONFIG_FILE "${CMAKE_CURRENT_LIST_DIR}/${BOARD}.cmake")

if (EXISTS ${BOARD_CONFIG_FILE})
    include(${BOARD_CONFIG_FILE})
else()
    message(FATAL_ERROR "Board file not found: ${BOARD_CONFIG_FILE}")
endif()

if (NOT DEFINED FLASH_SIZE_MB)
    message("Flash size not defined, defaulting to 2 MB")
    set(FLASH_SIZE_MB 2)
endif()

# split flash in 5 partitions: boot, fw, update, settings, apps

math(EXPR PICO_FLASH_SIZE_BYTES "${FLASH_SIZE_MB} * 1024 * 1024")

math(EXPR BOOTLOADER_PARTITION_SIZE_BYTES "32 * 1024") # 32 KiB

math(EXPR FW_PARTITION_OFFSET "${BOOTLOADER_PARTITION_SIZE_BYTES}" OUTPUT_FORMAT HEXADECIMAL)
math(EXPR FW_PARTITION_SIZE_BYTES "(680 * 1024) - ${BOOTLOADER_PARTITION_SIZE_BYTES}") # 648 KiB

math(EXPR UPDATE_PARTITION_OFFSET "${BOOTLOADER_PARTITION_SIZE_BYTES} + ${FW_PARTITION_SIZE_BYTES}" OUTPUT_FORMAT HEXADECIMAL)
set(UPDATE_PARTITION_SIZE_BYTES ${FW_PARTITION_SIZE_BYTES}) # 648 KiB

math(EXPR SETTINGS_PARTITION_OFFSET "${BOOTLOADER_PARTITION_SIZE_BYTES} + ${FW_PARTITION_SIZE_BYTES} + ${UPDATE_PARTITION_SIZE_BYTES}" OUTPUT_FORMAT HEXADECIMAL) # 8 KiB
math(EXPR WEAR_LEVELING_RP2040_FLASH_BASE "${SETTINGS_PARTITION_OFFSET}" OUTPUT_FORMAT HEXADECIMAL)
set(WEAR_LEVELING_BACKING_SIZE 8192) # 8 KiB
math(EXPR SETTINGS_PARTITION_SIZE_BYTES "${WEAR_LEVELING_BACKING_SIZE}")

math(EXPR APPS_PARTITION_OFFSET "${BOOTLOADER_PARTITION_SIZE_BYTES} + ${FW_PARTITION_SIZE_BYTES} + ${UPDATE_PARTITION_SIZE_BYTES} + ${SETTINGS_PARTITION_SIZE_BYTES}" OUTPUT_FORMAT HEXADECIMAL)

if(FLASH_SIZE_MB EQUAL 2)
    # 32 + 648 + 648 + 8 + 712 KiB
    math(EXPR APPS_PARTITION_SIZE_BYTES "712 * 1024") # 712 KiB
elseif(FLASH_SIZE_MB EQUAL 4)
    # 32 + 648 + 648 + 8 + 2760 KiB
    math(EXPR APPS_PARTITION_SIZE_BYTES "2760 * 1024") # 2760 KiB
elseif(FLASH_SIZE_MB EQUAL 8)
    # 32 + 648 + 648 + 8 + 6856 KiB
    math(EXPR APPS_PARTITION_SIZE_BYTES "6856 * 1024") # 6856 KiB
elseif(FLASH_SIZE_MB EQUAL 16)
    # 32 + 648 + 648 + 8 + 15048 KiB
    math(EXPR APPS_PARTITION_SIZE_BYTES "15048 * 1024") # 15048 KiB
else()
    # max supported flash size by RP2040 is 16 MB (see 2.6.3.1 in datasheet)
    message(FATAL_ERROR "Unsupported flash size")
endif()
