set(BOARD_CONFIG_FILE "${CMAKE_CURRENT_LIST_DIR}/${BOARD}.cmake")

if (EXISTS ${BOARD_CONFIG_FILE})
    include(${BOARD_CONFIG_FILE})
else()
    message(FATAL_ERROR "Board file not found: ${BOARD_CONFIG_FILE}")
endif()

if (NOT DEFINED FLASH_SIZE_MB)
    message("Flash size not defined, defaulting to 2 MB")
    set(FLASH_SIZE_MB 2)
endif()

# split flash in 3 parts to keep FW and apps separate,
# have an update partition to be able to atomicaly update either partition,
# also reserve a bit of space for keeping settings

set(PICO_FLASH_SIZE_BYTES "680 * 1024") # 0x10000000, 680 KB

if(FLASH_SIZE_MB EQUAL 2)
    math(EXPR APPS_PARTITION_OFFSET "0x10000000 + (680 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 680 KB
    math(EXPR UPDATE_PARTITION_OFFSET "0x10000000 + (2 * 680 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 680 KB
    math(EXPR SETTINGS_PARTITION_OFFSET "0x10000000 + (3 * 680 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 8 KB
elseif(FLASH_SIZE_MB EQUAL 4)
    math(EXPR APPS_PARTITION_OFFSET "0x10000000 + (680 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 1704 KB
    math(EXPR UPDATE_PARTITION_OFFSET "0x10000000 + (680 * 1024) + (1 * 1704 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 1704 KB
    math(EXPR SETTINGS_PARTITION_OFFSET "0x10000000 + (680 * 1024) + (2 * 1704 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 8 KB
elseif(FLASH_SIZE_MB EQUAL 8)
    math(EXPR APPS_PARTITION_OFFSET "0x10000000 + (680 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 3752 KB
    math(EXPR UPDATE_PARTITION_OFFSET "0x10000000 + (680 * 1024) + (1 * 3752 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 3752 KB
    math(EXPR SETTINGS_PARTITION_OFFSET "0x10000000 + (680 * 1024) + (2 * 3752 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 8 KB
elseif(FLASH_SIZE_MB EQUAL 16)
    math(EXPR APPS_PARTITION_OFFSET "0x10000000 + (680 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 7848 KB
    math(EXPR UPDATE_PARTITION_OFFSET "0x10000000 + (680 * 1024) + (1 * 7848 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 7848 KB
    math(EXPR SETTINGS_PARTITION_OFFSET "0x10000000 + (680 * 1024) + (2 * 7848 * 1024)" OUTPUT_FORMAT HEXADECIMAL) # 8 KB
else()
    # max supported flash size by RP2040 is 16 MB (see 2.6.3.1 in datasheet)
    message(FATAL_ERROR "Unsupported flash size")
endif()
