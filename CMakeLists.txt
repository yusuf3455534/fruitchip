cmake_minimum_required(VERSION 3.26)

project(fruitchip)

include(ExternalProject)

set(CMAKE_EXPORT_COMPILE_COMMANDS 1)

set(LOADER_INSTALL_PREFIX ${PROJECT_BINARY_DIR}/loader)
set(FWFS_INSTALL_PREFIX ${PROJECT_BINARY_DIR}/fwfs)
set(MENU_INSTALL_PREFIX ${PROJECT_BINARY_DIR}/menu)
set(MASSWATCH_INSTALL_PREFIX ${PROJECT_BINARY_DIR}/masswatch)

if(NOT DEFINED BOARD)
    message(FATAL_ERROR "board not selected")
endif()

if(NOT DEFINED BUILD_PS2)
    set(BUILD_PS2 1)
endif()

if(NOT DEFINED BUILD_ARM)
    set(BUILD_ARM 1)
endif()

message("BUILD_PS2 ${BUILD_PS2}")
message("BUILD_ARM ${BUILD_ARM}")

if(BUILD_PS2)
    ExternalProject_Add(
        fruitchip-loader-ee
        SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/fruitchip-loader/ee"
        CMAKE_ARGS
            -DCMAKE_EXPORT_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}
            -DCMAKE_TOOLCHAIN_FILE=$ENV{PS2SDK}/ps2dev.cmake
            -DCMAKE_INSTALL_PREFIX=${LOADER_INSTALL_PREFIX}
        INSTALL_BYPRODUCTS
            ${LOADER_INSTALL_PREFIX}/fruitchip-loader-ee-stage1.bin
            ${LOADER_INSTALL_PREFIX}/fruitchip-loader-ee-stage2.bin
        BUILD_ALWAYS 1
    )

    ExternalProject_Add(
        fruitchip-fw-apps
        SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/fruitchip-fw-apps"
        CMAKE_ARGS
            -DBOARD=${BOARD}
            -DPICO_PLATFORM=host
        INSTALL_COMMAND cmake -E true
        BUILD_ALWAYS 1
    )

    ExternalProject_Add(
        fruitchip-fwfs
        SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/fruitchip-fwfs"
        CMAKE_ARGS
            -DCMAKE_EXPORT_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}
            -DCMAKE_TOOLCHAIN_FILE=${CMAKE_CURRENT_SOURCE_DIR}/ps2dev_iop.cmake
            -DCMAKE_INSTALL_PREFIX=${FWFS_INSTALL_PREFIX}
        BUILD_ALWAYS 1
    )

    ExternalProject_Add(
        masswatch-iop
        SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/fruitchip-menu/lib/masswatch/iop"
        CMAKE_ARGS
            -DCMAKE_EXPORT_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}
            -DCMAKE_TOOLCHAIN_FILE=${CMAKE_CURRENT_SOURCE_DIR}/ps2dev_iop.cmake
            -DCMAKE_INSTALL_PREFIX=${MASSWATCH_INSTALL_PREFIX}
        BUILD_ALWAYS 1
    )

    ExternalProject_Add(
        masswatch-ee
        SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/fruitchip-menu/lib/masswatch/ee"
        CMAKE_ARGS
            -DCMAKE_EXPORT_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}
            -DCMAKE_TOOLCHAIN_FILE=$ENV{PS2SDK}/ps2dev.cmake
            -DCMAKE_INSTALL_PREFIX=${MASSWATCH_INSTALL_PREFIX}
        BUILD_ALWAYS 1
    )

    ExternalProject_Add(
        fruitchip-menu
        SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/fruitchip-menu"
        CMAKE_ARGS
            -DBOARD=${BOARD}
            -DCMAKE_EXPORT_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}
            -DCMAKE_TOOLCHAIN_FILE=$ENV{PS2SDK}/ps2dev.cmake
            -DCMAKE_INSTALL_PREFIX=${MENU_INSTALL_PREFIX}
            -DFWFS_INSTALL_PREFIX=${FWFS_INSTALL_PREFIX}
            -DMASSWATCH_INSTALL_PREFIX=${MASSWATCH_INSTALL_PREFIX}
        DEPENDS
            fruitchip-fwfs
            masswatch-iop
            masswatch-ee
        BUILD_ALWAYS 1
    )

    ExternalProject_Add(
        fruitchip-test-ps2
        SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/fruitchip-test-ps2"
        CMAKE_ARGS
            -DCMAKE_EXPORT_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}
            -DCMAKE_TOOLCHAIN_FILE=$ENV{PS2SDK}/ps2dev.cmake
            -DFWFS_INSTALL_PREFIX=${FWFS_INSTALL_PREFIX}
        INSTALL_COMMAND cmake -E true
        BUILD_ALWAYS 1
    )
endif()

if(BUILD_ARM)
    ExternalProject_Add(
        fruitchip-fw
        SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/fruitchip-fw"
        CMAKE_ARGS
            -DBOARD=${BOARD}
            -DCMAKE_EXPORT_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}
            -DLOADER_INSTALL_PREFIX=${LOADER_INSTALL_PREFIX}
            -DMENU_INSTALL_PREFIX=${MENU_INSTALL_PREFIX}
        INSTALL_COMMAND cmake -E true
        # helps to avoid reconfiguring the project due to rebuilding loader, even if there were no changes
        CONFIGURE_HANDLED_BY_BUILD 1
        BUILD_ALWAYS 1
    )

    if(BUILD_PS2)
        ExternalProject_Add_StepDependencies(fruitchip-fw build fruitchip-loader-ee)
        ExternalProject_Add_StepDependencies(fruitchip-fw build fruitchip-menu)
    endif()

    ExternalProject_Add(
        fruitchip-fw-bootloader
        SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/fruitchip-fw-bootloader"
        CMAKE_ARGS
            -DBOARD=${BOARD}
            -DCMAKE_EXPORT_COMPILE_COMMANDS=${CMAKE_EXPORT_COMPILE_COMMANDS}
        INSTALL_COMMAND cmake -E true
        BUILD_ALWAYS 1
    )
endif()

if(CMAKE_EXPORT_COMPILE_COMMANDS)
    if(BUILD_PS2 OR BUILD_ARM)
        file(READ ${CMAKE_CURRENT_SOURCE_DIR}/.clangd.in CLANGD_CONFIG_TEMPLATE)

        file(WRITE ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "# Generated by CMake\n\n")
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd ${CLANGD_CONFIG_TEMPLATE})
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "\n")
    endif()

    macro(append_compilation_database_to_clangd_config TARGET)
        ExternalProject_Get_property(${TARGET} SOURCE_DIR)
        cmake_path(RELATIVE_PATH SOURCE_DIR OUTPUT_VARIABLE SOURCE_DIR_REL)

        ExternalProject_Get_property(${TARGET} BINARY_DIR)
        cmake_path(RELATIVE_PATH BINARY_DIR OUTPUT_VARIABLE BINARY_DIR_REL)

        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "---")
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "\n")
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "\n")
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "If:\n")
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "  PathMatch: ${SOURCE_DIR_REL}/.*\n")
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "\n")
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "CompileFlags:\n")
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "  CompilationDatabase: ${BINARY_DIR_REL}\n")
        file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/.clangd "\n")
    endmacro()

    if(BUILD_PS2)
        append_compilation_database_to_clangd_config(fruitchip-fwfs)
        append_compilation_database_to_clangd_config(fruitchip-menu)
        append_compilation_database_to_clangd_config(fruitchip-loader-ee)
        append_compilation_database_to_clangd_config(masswatch-iop)
        append_compilation_database_to_clangd_config(masswatch-ee)
        append_compilation_database_to_clangd_config(fruitchip-test-ps2)
    endif()

    if(BUILD_ARM)
        append_compilation_database_to_clangd_config(fruitchip-fw-bootloader)
        append_compilation_database_to_clangd_config(fruitchip-fw)
    endif()
endif()
